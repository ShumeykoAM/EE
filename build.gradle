plugins {
  id 'java'
  id 'war'
  id 'idea'
}

version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  def lombok = 'org.projectlombok:lombok:1.18.22'
  compileOnly lombok
  annotationProcessor lombok
  testCompileOnly lombok
  testAnnotationProcessor lombok

  implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
}

war {
  archiveName = "${AppName}.war"
}

//Деплой на GlassFish
task autoDeployGlassFish(dependsOn: 'war') {
  doLast {
    if (System.getenv('GLASSFISH_HOME') == null)
    {
      println("WARNING: Deployment in GlassFish is not possible, cause not set environment variable GLASSFISH_HOME")
    }
    else
    {
      copy {
        def fromPath = "${war.archivePath}"
        def intoPath = "${System.env.GLASSFISH_HOME}${File.separator}glassfish${File.separator}domains${File.separator}${DomainName}${File.separator}autodeploy"
        println('copy from ' + fromPath)
        println('copy to ' + intoPath)
        from(fromPath)
        into(intoPath)
      }
      //Открываем браузер
      java.awt.Desktop.desktop.browse "http://localhost:4848".toURI()
    }
  }
}

//Этот деплой на GlassFish работает только когда сервак уже запущен, так что он для нас бесполезен, оставил как пример работы с командной строкой
task deployGlassFish(dependsOn: 'build', type: Exec) {
  if (System.getenv('GLASSFISH_HOME') == null)
  {
    println("WARNING: Deployment is not possible, cause not set environment variable GLASSFISH_HOME")
    enabled = false //Отключим таск, он не будет выполняться (doFist и doLast)
  }
  else
  {
    //TODO Пример использования командной строки в разных ОС
    println("${System.env.GLASSFISH_HOME}${File.separator}bin")
    println("${war.archivePath}")

    workingDir "${System.env.GLASSFISH_HOME}${File.separator}bin"
    if (System.properties['os.name'].toLowerCase().contains('windows'))
    {
      commandLine 'cmd', '/c', 'asadmin.bat'
    }
    else
    {
      commandLine "./asadmin"
    }
    args "deploy", "--force=true", "${war.archivePath}"
  }
}